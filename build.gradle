import org.apache.tools.ant.filters.EscapeUnicode

buildscript {
    ext.kotlin_version = '1.2.31'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'org.glavo'
version '4.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'
sourceCompatibility = 1.8

if (sourceCompatibility.isJava9Compatible()) {
    sourceSets {
        main {
            java {
                srcDirs += 'src/main/java9'
            }

            kotlin {
                srcDirs += 'src/main/java9'
            }
        }
    }
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/java'
}

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // https://mvnrepository.com/artifact/org.jetbrains.kotlinx/kotlinx-coroutines-javafx
    //compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '0.22.5'

    // https://mvnrepository.com/artifact/org.fxmisc.richtext/richtextfx
    compile group: 'org.fxmisc.richtext', name: 'richtextfx', version: '0.8.2'

    compile 'com.google.code.gson:gson:2.8.2'

    // https://mvnrepository.com/artifact/com.github.salomonbrys.kotson/kotson
    compile group: 'com.github.salomonbrys.kotson', name: 'kotson', version: '2.5.0'

}


compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"
javadoc.options.encoding = "UTF-8"

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes 'Implementation-Version': '1.2',
                'Main-Class': 'org.glavo.viewer.gui.Viewer'
    }
}

task fatJar(type: Jar) {
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    manifest {
        attributes 'Implementation-Version': '1.2',
                'Main-Class': 'org.glavo.viewer.gui.Viewer'
    }
    with jar
}

task run(type: JavaExec, dependsOn: fatJar) {
    main = "-jar"
    //noinspection GroovyAssignabilityCheck
    args fatJar.archivePath
}

tasks.withType(ProcessResources).each { task ->
    task.from(task.getSource()) {
        include '**/*.properties'
        filter(EscapeUnicode)
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}


